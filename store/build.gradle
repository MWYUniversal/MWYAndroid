//注意gradle.properties中的数据类型都是String类型，使用其他数据类型需要自行转换
if (isStoreApplication.toBoolean()) {
    //构建后输出一个 APK 安装包
    apply plugin: 'com.android.application'
} else {
    //构建后输出 ARR 包
    apply plugin: 'com.android.library'
}
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.protobuf'


/*plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}*/

android {
    namespace 'com.owner.store'
    compileSdk androidx.versions.compileSdk.get().toInteger()

    defaultConfig {
        if (isStoreApplication.toBoolean()) {
            //独立调试的时候才能设置applicationId
            applicationId "com.owner.store"
        }
        minSdk androidx.versions.minSdk.get().toInteger()
        targetSdk androidx.versions.targetSdk.get().toInteger()
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            if (isStoreApplication.toBoolean()) {
                //独立调试
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                //集成调试
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
        dataBinding = true
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //DataStore 相关
    implementation 'androidx.datastore:datastore-preferences:1.0.0'
    implementation 'androidx.datastore:datastore-preferences-core:1.0.0'

    //Proto DataStore
    implementation  'androidx.datastore:datastore-core:1.0.0'
    implementation  'com.google.protobuf:protobuf-javalite:3.10.0'

}

//配置协议缓冲区
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    // 为该项目中的 Protobufs 生成 java Protobuf-lite 代码。
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
