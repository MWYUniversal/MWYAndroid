//注意gradle.properties中的数据类型都是String类型，使用其他数据类型需要自行转换
if(isFileApplication.toBoolean()){
    //构建后输出一个 APK 安装包
    apply plugin: 'com.android.application'
}else{
    //构建后输出 ARR 包
    apply plugin: 'com.android.library'
}
apply  plugin: 'org.jetbrains.kotlin.android'
/*
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}*/

android {
    namespace 'com.owner.file'
    compileSdk 33

    defaultConfig {
        if(isFileApplication.toBoolean()) {
            applicationId "com.owner.file"
        }
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            if (isFileApplication.toBoolean()) {
                //独立调试
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                //集成调试
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //这里暂时用https://github.com/javakam/FileOperator/tree/master的文件操作库
    implementation 'com.github.javakam:file.core:3.6.0@aar'      //核心库必选(Core library required)
    implementation 'com.github.javakam:file.selector:3.6.0@aar'  //文件选择器(File selector)
    implementation 'com.github.javakam:file.compressor:3.6.0@aar'//图片压缩, 修改自Luban(Image compression, based on Luban)
}